title: Protocol Selectbox
ns: json-editor
name: protocol
id: MyProtocol

Log(console/log)
ProtocolEl(dom/querySelector), CommandEl(dom/querySelector)
EditorEl(dom/querySelector)
ButtonEl(dom/querySelector)

SelectProtocol(bootstrap/select), SelectCommand(bootstrap/select)
ClearCommands(dom/setHtml)
ClearEditor(dom/setHtml)

CommandEditor(json-editor/editor)
Validate(json-editor/validate)
Button(bootstrap/button)

# TODO: relative paths should always work.
provider ./graph/{ns}/{name}.fbp as x

GraphCommands(x:models/model)

# rule (for now): persist never starts
'#protocol' -> @selector ProtocolEl selection -> element SelectProtocol
'#command'  -> @selector CommandEl selection -> ^element SelectCommand
'#editor'   -> @selector EditorEl selection -> ^element CommandEditor
'#button'   -> @selector ButtonEl selection -> element Button

'Protocol' -> @label SelectProtocol
'Command'  -> @label SelectCommand

"./defaults.json!" ~> @options CommandEditor

'' -> @html ClearCommands
'' -> @html ClearEditor

CommandEl selection -> ^element ClearCommands
EditorEl selection -> ^element ClearEditor

[
  { "label": "Select..", "value": "clear" },
  { "label": "Graph", "value": "graph" },
  { "label": "Network", "value": "network" },
  { "label": "Runtime", "value": "runtime" },
  { "label": "Component", "value": "component" }
] -> @options SelectProtocol

SelectProtocol out -> in GraphCommands
SelectProtocol out [clear] -> :start ClearCommands
SelectProtocol out -> :start ClearEditor
GraphCommands options     -> options SelectCommand

CommandEditor out -> in Button

# weird, it continues if editor is not set, it should never reach the portbox..
CommandEditor editor -> editor Validate

# log to console.
Button out -> msg Log
Button out -> in Validate
Validate errors -> msg Log
Validate out -> msg Log

SelectCommand out -> command GraphCommands
GraphCommands schema -> schema CommandEditor
GraphCommands schema -> msg Log
